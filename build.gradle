plugins {
  id 'com.github.johnrengelman.shadow' version '2.0.2'
  id 'java'
  id 'application'
  id 'groovy'
}

mainClassName = "org.karaffe.compiler.launcher.KaraffeCompilerLauncher"

ext {
  junitVersion = '4.12'
  junit5Version = '5.1.0'
  logbackVersion = '1.2.3'
}

dependencies {
  compile subprojects
}

allprojects {
  repositories {
    mavenCentral()
    jcenter()
  }
}

subprojects {
  apply plugin: 'com.github.johnrengelman.shadow'
  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'groovy'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  dependencies {
    compile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    compile group: 'net.arnx', name: 'jsonic', version: '1.3.10'
  }

  tasks.withType(AbstractCompile)*.options*.encoding = tasks.withType(GroovyCompile)*.groovyOptions*.encoding = 'UTF-8'

  jacocoTestReport {
    reports {
      xml.enabled true
    }
  }

  test {
    testLogging {
      events 'passed', 'failed', 'skipped'
    }
  }
}

processResources {
  from 'resources'
}

apply from: 'gradle/project_structure.gradle'
apply from: 'gradle/detect_java_version.gradle'

project(':backend') {
  dependencies {
    compile group: 'net.nokok', name: 'azm', version: '0.1.6'
  }
}

project(':base') {
  dependencies {
    compile group: 'args4j', name: 'args4j', version: '2.33'
    compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.1'
  }
}

project(':corelib') {
}

project(':launcher') {

}

project(':frontend') {
  apply plugin: 'antlr'
  dependencies {
    antlr "org.antlr:antlr4:4.7.1"
  }

  generateGrammarSource {
    arguments += [
            '-Xlog',
            '-long-messages',
            '-visitor',
            '-listener',
            '-package', 'org.karaffe.compiler.frontend.karaffe.antlrautogenerated'
    ]
    outputDirectory = file("${projectDir}/src/main/java/org/karaffe/compiler/frontend/karaffe/antlrautogenerated")
  }
}

project(':test') {
  dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.15'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
    testCompile group: 'junit', name: 'junit', version: junitVersion
  }
}

startScripts {
  applicationName = 'krfc'
}

startShadowScripts {
  applicationName = 'krfc'
}

distributions {
  main {
    baseName = 'Karaffe-compiler'
  }
}

test.dependsOn 'installDist'
